# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../../scripts/build/ninja/devtools_entrypoint.gni")
import("../../../scripts/build/ninja/devtools_module.gni")
import("../../../scripts/build/ninja/devtools_pre_built.gni")
import("../../../scripts/build/ninja/node.gni")
import("../../../scripts/build/ninja/vars.gni")
import("../../../scripts/build/typescript/typescript.gni")

import("//$devtools_root_location/config/gni/i18n.gni")

devtools_module("i18n") {
  sources = [
    "ByteUtilities.ts",
    "DevToolsLocale.ts",
    "NumberFormatter.ts",
    "i18nImpl.ts",
    "i18nTypes.ts",
    "time-utilities.ts",
  ]

  deps = [
    ":locales_js",
    "../../core/platform:bundle",
    "../../core/root:bundle",
    "../../third_party/i18n:bundle",
    "../../third_party/intl-messageformat:bundle",
  ]
}

devtools_entrypoint("bundle") {
  entrypoint = "i18n.ts"
  deps = [ ":i18n" ]

  visibility = [
    # IMPORTANT: Be careful when extending visibility of front_end/core/i18n,
    # as the i18n module should only be used in UI code, and business logic
    # should never depend on the i18n module.
    ":*",
    "../../entrypoints/*",
    "../../panels/*",
    "../../ui/*",

    # TODO(crbug/452597733): remove these dependencies.
    "../../core/common/*",
    "../../core/host/*",
    "../../core/sdk/*",
    "../../models/ai_assistance/*",
    "../../models/bindings/*",
    "../../models/emulation/*",
    "../../models/extensions/*",
    "../../models/har/*",
    "../../models/issues_manager/*",
    "../../models/logs/*",
    "../../models/network_time_calculator/*",
    "../../models/persistence/*",
    "../../models/workspace/*",

    # TODO(crbug/452597733): testing should be split up for UI/non-UI testing utilities.
    "../../testing/*",
  ]
}

node_action("generate_locales_js") {
  script = "front_end/core/i18n/generate-locales-js.js"

  args = [
    "--target-gen-dir",
    rebase_path(target_gen_dir, root_build_dir),
  ]
  args += [ "--remote-locales" ]
  args += remote_devtools_locales

  args += [ "--bundled-locales" ]
  args += bundled_devtools_locales

  args += [
    "--default-locale",
    devtools_default_locale,
  ]

  args += [
    "--remote-fetch-pattern",
    remote_fetch_pattern,
  ]

  args += [
    "--local-fetch-pattern",
    local_fetch_pattern,
  ]

  outputs = [ "$target_gen_dir/locales.js" ]
  data = outputs

  if (!devtools_bundle) {
    metadata = {
      grd_files = outputs
    }
  }
}

devtools_pre_built("locales_js") {
  sources = [
    "locales.d.ts",
    "locales_js-tsconfig.json",
  ]

  deps = [ ":generate_locales_js" ]
}

ts_library("unittests") {
  testonly = true

  sources = [ "i18nImpl.test.ts" ]

  deps = [
    ":bundle",
    "../../testing",
  ]
}

devtools_foundation_module("foundation_unittests") {
  testonly = true

  sources = [
    "ByteUtilities.test.ts",
    "DevToolsLocale.test.ts",
    "NumberFormatter.test.ts",
    "i18n.test.ts",
    "time-utilities.test.ts",
  ]

  deps = [
    ":bundle",
    "../../testing",
  ]
}

# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../../scripts/build/typescript/typescript.gni")

template("devtools_base_module") {
  ts_library(target_name) {
    forward_variables_from(invoker, "*", [ "visibility" ])

    reset_timestamps = true

    visibility += [ ":*" ]
  }
}

template("devtools_module") {
  devtools_base_module(target_name) {
    forward_variables_from(invoker, "*")
  }
}

# A module that can run in any runtime we support. As such, it must only use
# features and APIs shared across all supported runtimes.
template("devtools_foundation_module") {
  devtools_base_module(target_name) {
    forward_variables_from(invoker, "*")

    es_target = "es2023"
    es_libs = []
  }
}

template("devtools_ui_module") {
  devtools_base_module(target_name) {
    forward_variables_from(invoker, "*")

    es_target = "esnext"
    es_libs = [
      "dom",
      "dom.iterable",
    ]
  }
}

foreach(module,
        [
          "devtools_foundation_module",
          "devtools_module",
          "devtools_ui_module",
        ]) {
  set_defaults(module) {
    visibility = []
    data = []
    public_deps = []
  }
}
